import org.gradle.api.plugins.jvm.JvmTestSuite
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import com.github.jengelman.gradle.plugins.shadow.transformers.PropertiesFileTransformer

plugins {
    id 'java-library'
    id 'jvm-test-suite'
    id 'idea'
    id 'eclipse'
    id 'maven-publish'
    alias libs.plugins.gradleutils
    alias libs.plugins.gitversion
    alias libs.plugins.changelog
    alias libs.plugins.licenser
    alias libs.plugins.shadow
}

gradleutils.displayName = 'Forge Auto Renaming Tool' // 'Renamer'
description = 'A command line tool to rename java class files'
group = 'net.minecraftforge'
version = gitversion.tagOffset

println "Version: $version"

java {
    toolchain.languageVersion = JavaLanguageVersion.of(8)
    withSourcesJar()
}

testing {
    suites.named('test', JvmTestSuite) {
        useJUnitJupiter(libs.versions.junit)

        dependencies {
            implementation libs.junit.api
            runtimeOnly.bundle libs.bundles.junit.runtime
            implementation libs.powermock
        }

        targets.configureEach {
            testTask.configure {
                testLogging {
                    events 'passed', 'skipped', 'failed'
                }
            }
        }
    }
}

dependencies {
    compileOnly libs.annotations.jetbrains

    implementation libs.jopt
    api libs.srgutils
    implementation libs.bundles.asm
}

tasks.named('jar', Jar) {
    manifest {
        attributes('Main-Class': 'net.minecraftforge.fart.Main')
        attributes([
            'Specification-Title'   : 'ForgeAutoRenamingTool',
            'Specification-Vendor'  : gradleutils.vendor.get(),
            'Specification-Version' : gitversion.info.tag,
            'Implementation-Title'  : 'ForgeAutoRenamingTool',
            'Implementation-Version': project.version,
            'Implementation-Vendor' : gradleutils.vendor.get()
        ], 'net/minecraftforge/fart/')
    }
}

tasks.named('shadowJar', ShadowJar) {
    enableAutoRelocation = true
    relocationPrefix = 'net.minecraftforge.fart.relocated'

    minimize()

    // Rewrite JOpt's message files, so that help text is displayed nicely.
    transform(PropertiesFileTransformer) {
        paths = ['Messages.properties$']
        keyTransformer = { key -> "net.minecraftforge.fart.relocated.$key".toString() }
    }
}

changelog {
    from '0.1'
}

license {
    header = file('LICENSE-header.txt')
    newLine = false
    exclude '**/*.properties'
}

publishing {
    repositories {
        maven gradleutils.publishingForgeMaven
    }

    publications.register('mavenJava', MavenPublication) {
        from components.java

        pom {
            name = 'Forge Auto Renaming Tool'
            description = 'A tool that renames java bytecode elements.'
            url = 'https://github.com/MinecraftForge/ForgeAutoRenamingTool'

            gradleutils.pom.addRemoteDetails(pom)

            license gradleutils.pom.licenses.LGPLv2_1

            developers {
                developer gradleutils.pom.developers.LexManos
            }
        }
    }
}
